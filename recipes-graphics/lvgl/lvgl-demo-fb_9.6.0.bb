SUMMARY = "LVGL Demo Application for Framebuffer"
HOMEPAGE = "https://github.com/lvgl/lv_port_linux_frame_buffer"

LICENSE = "MIT"
LIC_FILES_CHKSUM = "file://LICENSE;md5=802d3d83ae80ef5f343050bf96cce3a4 \
                    file://lvgl/LICENCE.txt;md5=4570b6241b4fced1d1d18eb691a0e083"

DEPENDS = "python3-pcpp-native"

PV .= "+git"

SRC_URI = "\
    git://github.com/lvgl/lv_port_linux_frame_buffer.git;protocol=https;branch=release/v9.6;name=demo \
    git://github.com/lvgl/lvgl;protocol=https;branch=release/v9.6;name=lvgl;subdir=git/lvgl \
"

SRCREV_demo = "0728bc4eff0ff85ef787db3f557a654c5c7c9c41"
SRCREV_lvgl = "60b614c23c816ca5edc5f2840c9945eff7da0ad4"
SRCREV_FORMAT = "demo_lvgl"
# The version check runs against the first SRC_URI (the demo repo), whose tags
# are "vX.Y.Z" but top out at v9.2.2 -- below PV 9.5.0+git, which follows the
# bundled lvgl release. Regex is correct; marked unknown until the demo repo
# tags a >=9.5.0 release.
UPSTREAM_CHECK_GITTAGREGEX = "v(?P<pver>\d+(\.\d+)+)"
UPSTREAM_VERSION_UNKNOWN = "1"

inherit cmake pkgconfig

S = "${WORKDIR}/git"

require lv-conf.inc

do_install() {
    install -d ${D}${bindir}
    install -m 0755 ${B}/bin/lvglsim ${D}${bindir}
}
